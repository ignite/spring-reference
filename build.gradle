apply plugin: 'war'
apply plugin: 'jetty'
apply plugin: 'eclipse'
apply plugin: 'eclipse-wtp'
apply plugin: 'maven'
apply from: "https://launchpad.net/gradle-release/trunk/latest/+download/apply.groovy"
apply from: 'https://github.com/valkolovos/gradle_cobertura/raw/master/repo/gradle_cobertura/gradle_cobertura/1.0/coberturainit.gradle'

sourceCompatibility = '1.6'
stopKey = 'gfshrgerthjrwh'
stopPort = 9451
httpPort = 8080

sourceSets {
	main {
		output.resourcesDir = "build/classes/main"
	}
	test {
		resources {
			srcDirs = ['src/test/resources', 'src/main/webapp/WEB-INF/spring']
		}
	}
	restTest {
		java.srcDir file('src/rest-test/java')
		output.classesDir = "build/classes/rest-test"
	}
	uiTest {
		java.srcDir file('src/ui-test/java')
		output.classesDir = "build/classes/ui-test"
	}
}

repositories {

    mavenCentral()

    maven {
        url "https://id.smartlink.ee/repo/mvnrepo"
    }
	
	maven {
		url "https://repository-saucelabs.forge.cloudbees.com/release"
	}
}

artifacts {  archives war }

configurations {
	all*.exclude group: 'javax.mail', module: 'mail'
	all*.exclude group: 'javax.jms', module: 'jms'
	all*.exclude group: 'com.sun.jdmk', module: 'jmxtools'
	all*.exclude group: 'com.sun.jmx', module: 'jmxri'
	all*.exclude group: 'commons-logging', module: 'commons-logging'
	all*.exclude group: 'xerces', module: 'xercesImpl'
	deployerJars
}

dependencies {
    compile(
        'org.springframework:spring-context:3.1.1.RELEASE',
        'org.springframework:spring-webmvc:3.1.1.RELEASE',
        'org.springframework:spring-orm:3.1.1.RELEASE',
        'org.springframework:spring-oxm:3.1.1.RELEASE',
        'org.springframework:spring-aop:3.1.1.RELEASE',
        'org.springframework:spring-context-support:3.1.1.RELEASE',
        'org.springframework.security:spring-security-core:3.1.1.RELEASE',
        'org.springframework.security:spring-security-config:3.1.1.RELEASE',
        'org.springframework.security:spring-security-web:3.1.1.RELEASE',
        'org.springframework.security:spring-security-openid:3.1.1.RELEASE',
        'org.springframework.security:spring-security-taglibs:3.1.1.RELEASE',
        'org.aspectj:aspectjrt:1.6.9',
        'org.slf4j:slf4j-api:1.6.1',
        'javax.servlet:jstl:1.2',
        'org.hibernate:hibernate-entitymanager:3.6.0.Final',
        'org.hibernate:hibernate-validator:4.2.0.Final',
        'com.h2database:h2:1.3.156',
        'commons-lang:commons-lang:20030203.000129',
        'net.sourceforge.nekohtml:nekohtml:1.9.15',
        'org.springframework.data:spring-data-jpa:1.0.2.RELEASE',
        'org.codehaus.castor:castor-xml:1.3.2',
        'com.fasterxml.jackson.core:jackson-core:2.0.1',
        'com.fasterxml.jackson.module:jackson-module-jaxb-annotations:2.0.1',
        'ee.eesti.id:jdigidoc:2.3.99',
        'javax.inject:javax.inject:1'
    )

    runtime(
        'org.slf4j:jcl-over-slf4j:1.6.1',
        'org.slf4j:slf4j-log4j12:1.6.1',
        'log4j:log4j:1.2.15'
    )

    providedCompile(
        'javax.servlet:servlet-api:2.5',
        'javax.servlet.jsp:jsp-api:2.1',
    )

    testCompile(
        'org.springframework:spring-test:3.1.1.RELEASE',
        'junit:junit:4.10',
        'org.seleniumhq.selenium:selenium-java:2.25.0',
		'org.seleniumhq.selenium:selenium-server:2.25.0',
        'org.mockito:mockito-core:1.9.0-rc1',
        'com.saucelabs.selenium:selenium-client-factory:2.5',
        'com.saucelabs.selenium:sauce-ondemand-driver:2.5',
        'com.saucelabs.selenium:selenium-embedded-rc-driver:2.5'
    )
	
	deployerJars(
		'org.apache.maven.wagon:wagon-webdav:1.0-beta-2'
	)
	
	restTestCompile sourceSets.main.output
	restTestCompile configurations.testCompile
	restTestCompile sourceSets.test.output
	restTestRuntime configurations.testRuntime
	
	uiTestCompile sourceSets.main.output
	uiTestCompile configurations.testCompile
	uiTestCompile sourceSets.test.output
	uiTestRuntime configurations.testRuntime
}

uploadArchives {
	repositories.mavenDeployer {
		configuration = configurations.deployerJars
		repository(url: "dav:https://repository-reference.forge.cloudbees.com/release") {
			authentication(userName: "reference-app", password: "hWsyHpE7")
		}
		snapshotRepository(url: "dav:https://repository-reference.forge.cloudbees.com/snapshot") {
			authentication(userName: "reference-app", password: "hWsyHpE7")
		}
	}
}

jettyRunWar {
	daemon = true
}

task restTest(type: Test, dependsOn: jettyRunWar) {
	testClassesDir = sourceSets.restTest.output.classesDir
	classpath = sourceSets.restTest.runtimeClasspath
	testReportDir = file("build/reports/rest-test")
	testResultsDir = file("build/rest-test-results")
	systemProperties 'DEFAULT_SELENIUM_STARTING_URL': 'http://localhost:8080/spring-reference'
}

task uiTest(type: Test, dependsOn: jettyRunWar) {
	testClassesDir = sourceSets.uiTest.output.classesDir
	classpath = sourceSets.uiTest.runtimeClasspath
	testReportDir = file("build/reports/ui-test")
	testResultsDir = file("build/ui-test-results")
	maxParallelForks 2
	systemProperties 'DEFAULT_SELENIUM_STARTING_URL': 'http://localhost:8080/spring-reference'
	systemProperties 'DEFAULT_SELENIUM_DRIVER': 'embedded-rc:'
}

check.dependsOn restTest, uiTest, cobertura
coberturaXml.dependsOn cleanTest //https://github.com/valkolovos/gradle_cobertura/issues/18